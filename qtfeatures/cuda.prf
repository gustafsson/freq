usecuda {
DEFINES += USE_CUDA
SAWEROOT = $${qtfeatures}..

win32 {
    usecuda {
      TMPDIR = $$TMPDIR/cuda
    } else:useopencl {
      TMPDIR = $$TMPDIR/opencl
    } else {
      TMPDIR = $$TMPDIR/cpu
    }

    TMPDIR = tmp/$$TMPDIR

    win32:RCC_DIR = $$TMPDIR
    MOC_DIR = $$TMPDIR
    OBJECTS_DIR = $$TMPDIR/
    UI_DIR = $$TMPDIR


    CONFIG(debug, debug|release):OBJECTS_DIR = $${OBJECTS_DIR}debug/
    else:OBJECTS_DIR = $${OBJECTS_DIR}release/
}

CUDA_INCLUDEPATH = $$INCLUDEPATH
CUDA_INCLUDEPATH += \
    $$SAWEROOT/lib/gpumisc \
    $$SAWEROOT/src \

macx:CUDA_INCLUDEPATH += $$SAWEROOT/lib/sonicawe-maclib/include
win32:CUDA_INCLUDEPATH += $$SAWEROOT/lib/sonicawe-winlib

LIBS += -lcufft -lcudart -lcuda
CONFIG(debug, debug|release): CUDA_FLAGS += -g
CUDA_FLAGS += --use_fast_math
#CUDA_FLAGS += --ptxas-options=-v


CUDA_CXXFLAGS = $$QMAKE_CXXFLAGS
unix:testlib:CUDA_CXXFLAGS += -fPIC
CONFIG(debug, debug|release):CUDA_CXXFLAGS += $$QMAKE_CXXFLAGS_DEBUG
else:CUDA_CXXFLAGS += $$QMAKE_CXXFLAGS_RELEASE
win32 { 
    INCLUDEPATH += "$(CUDA_INC_PATH)"
    LIBS += -L"$(CUDA_LIB_PATH)"
    CUDA_CXXFLAGS -= -Zc:wchar_t-
    CUDA_CXXFLAGS += -Zc:wchar_t
    CUDA_CXXFLAGS += /EHsc
    cuda.output = $${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.obj
    cuda.commands = \"$(CUDA_BIN_PATH)/nvcc.exe\" \
		-ccbin $${QMAKE_CC} \
        -c \
        -Xcompiler \
        \"$$join(CUDA_CXXFLAGS," ")\" \
        $$join(CUDA_INCLUDEPATH,'" -I "','-I "','"') \
        $$CUDA_FLAGS \
        "${QMAKE_FILE_NAME}" \
        -m32 -o \
        "${QMAKE_FILE_OUT}"
}
unix:!macx {
    # auto-detect CUDA path
    # CUDA_DIR = $$system(which nvcc | sed 's,/bin/nvcc$,,')
    IS64 = $$system(if [ "`uname -m`" = "x86_64" ]; then echo 64; fi)
    CUDA_DIR = /usr/local/cuda
    INCLUDEPATH += $$CUDA_DIR/include
    QMAKE_LIBDIR += $$CUDA_DIR/lib$$IS64
    cuda.output = $${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.o
    cuda.commands = $${CUDA_DIR}/bin/nvcc \
		-ccbin $${QMAKE_CC} \
        -c \
        -Xcompiler \
        $$join(CUDA_CXXFLAGS,",") \
        $$join(CUDA_INCLUDEPATH,'" -I "$$SAWEROOT/src/','-I "$$SAWEROOT/src/','"') \
        $$CUDA_FLAGS \
        ${QMAKE_FILE_NAME} \
        -o \
        ${QMAKE_FILE_OUT}
    cuda.dependcy_type = TYPE_C
    cuda.depend_command_dosntwork = nvcc \
        -M \
        -Xcompiler \
        $$join(CUDA_CXXFLAGS,",") \
        $$join(CUDA_INCLUDEPATH,'" -I "','-I "','"') \
        ${QMAKE_FILE_NAME} \
        | \
        sed \
        "s,^.*: ,," \
        | \
        sed \
        "s,^ *,," \
        | \
        tr \
        -d \
        \\\\\\n
}

# cuda.depends = nvcc -M -Xcompiler $$join(QMAKE_CXXFLAGS,",") $$join(INCLUDEPATH,'" -I "','-I "','"') ${QMAKE_FILE_NAME} | sed "s,^.*: ,," | sed "s,^ *,," | tr -d '\\\n'

macx { 
    # auto-detect CUDA path
    # CUDA_DIR = $$system(which nvcc | sed 's,/bin/nvcc$,,')
    # manual
    CUDA_DIR = $$system(if [ -e /usr/local/cuda/bin/nvcc ]; then echo /usr/local/cuda; else echo /Developer/NVIDIA/CUDA-5.0; fi)
    INCLUDEPATH += $$CUDA_DIR/include
    QMAKE_LIBDIR += $$CUDA_DIR/lib
    CUDA_FLAGS += --no-align-double -m64
    cuda.output = ${OBJECTS_DIR}${QMAKE_FILE_BASE}_cuda.o
    cuda.commands = $${CUDA_DIR}/bin/nvcc \
		-ccbin $${QMAKE_CC} \
        -c \
        -Xcompiler \
        $$join(CUDA_CXXFLAGS,",")  \
        $$join(CUDA_INCLUDEPATH,'" -I "','-I "','"') \
        $$CUDA_FLAGS \
        ${QMAKE_FILE_NAME} \
        -o \
        ${QMAKE_FILE_OUT}
    cuda.dependcy_type = TYPE_C
}

cuda.input = CUDA_SOURCES
QMAKE_EXTRA_COMPILERS += cuda
} # usecuda
